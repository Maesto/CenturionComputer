; Centurion ConCATenate by ren14500. Public domain.
TITLE 'CAT'
SYSTEM ZCAT(MAIN,EXP=D,LL=200)
FILE CRT:SYSIPT,CLASS=0,SEQ
FILE INDEV:SYS000,CLASS=1,SEQ
FILE OUTDEV:SYS001,CLASS=1,SEQ
FILE INFILE:SYS000,CLASS=2,SEQ,BUFFER=400,*
FILE OUTFILE:SYS001,CLASS=2,SEQ,BUFFER=400,*
SET EOT:-2080374784
INTEGER COMBO
STRING CURRLINE(200)
FORMAT FULLLINE:C200
EXTERNAL STAT,IOERR,GUPSI
ENTRYPOINT CRT,INFILE,OUTFILE
ENTRY
; Open CRT for I/O errors.
OPEN IO(CRT)
; Choose the correct combination.
CALL GUPSI(COMBO)
IF(COMBO.EQ.0)CALL FTOF
IF(COMBO.EQ.1)CALL FTOD
IF(COMBO.EQ.2)CALL DTOF
IF(COMBO.EQ.3)CALL DTOD
; Close CRT. Exit the program with condition code 0 for success.
CLOSE CRT
STOP 0
; File -> file.
SUBROUTINE FTOF
  ; Open both as files.
  OPEN INPUT(INFILE)
  OPEN OUTPUT(OUTFILE)
  CALL STAT(1)
  FTOFLOOP:
    ; Read a line.
    READ(INFILE,FULLLINE)CURRLINE
    IF(STATUS.EQ.1)GO TO FTOFDONE
    IF(STATUS.NE.0)CALL IOERR
    ; Write the line.
    WRITE(OUTFILE,FULLLINE)CURRLINE
    IF(STATUS.NE.0)CALL IOERR
  GO TO FTOFLOOP
  FTOFDONE:
  ; Close files.
  ENDFILE OUTFILE
  CLOSE INFILE,OUTFILE
RETURN
; File -> device.
SUBROUTINE FTOD
  ; Open as file and device.
  OPEN INPUT(INFILE)
  OPEN OUTPUT(OUTDEV)
  CALL STAT(1)
  FTODLOOP:
    ; Read a line.
    READ(INFILE,FULLLINE)CURRLINE
    IF(STATUS.EQ.1)GO TO FTODDONE
    IF(STATUS.NE.0)CALL IOERR
    ; Write the line.
    WRITE(OUTDEV,FULLLINE)CURRLINE
    IF(STATUS.NE.0)CALL IOERR
  GO TO FTODLOOP
  FTODDONE:
  ; Close files.
  CLOSE INFILE,OUTDEV
RETURN
; Device -> file.
SUBROUTINE DTOF
  ; Open as device and file.
  OPEN INPUT(INDEV)
  OPEN OUTPUT(OUTFILE)
  CALL STAT(1)
  DTOFLOOP:
    ; Read a line.
    READ(INDEV,FULLLINE)CURRLINE
    IF(STATUS.EQ.1)GO TO DTOFDONE
    IF(STATUS.NE.0)CALL IOERR
    IFS(CURRLINE.EQ.EOT)GO TO DTOFDONE
    ; Write the line.
    WRITE(OUTFILE,FULLLINE)CURRLINE
    IF(STATUS.NE.0)CALL IOERR
  GO TO DTOFLOOP
  DTOFDONE:
  ; Close files.
  ENDFILE OUTFILE
  CLOSE INDEV,OUTFILE
RETURN
; Device -> device.
SUBROUTINE DTOD
  ; Open both as devices.
  OPEN INPUT(INDEV)
  OPEN OUTPUT(OUTDEV)
  CALL STAT(1)
  DTODLOOP:
    ; Read a line.
    READ(INDEV,FULLLINE)CURRLINE
    IF(STATUS.EQ.1)GO TO DTODDONE
    IF(STATUS.NE.0)CALL IOERR
    IFS(CURRLINE.EQ.EOT)GO TO DTODDONE
    ; Write the line.
    WRITE(OUTDEV,FULLLINE)CURRLINE
    IF(STATUS.NE.0)CALL IOERR
  GO TO DTODLOOP
  DTODDONE:
  ; Close files.
  CLOSE INDEV,OUTDEV
RETURN
END