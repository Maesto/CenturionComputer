s.lst zrv982 1 crt3
S.LST ZRV982 1 CRT3
.JOB ZRV982
/   ENTER LIBRARY NAME OR "NEWLINE" IF NONE /yrend90
LIST - 6.00
title ' zrv582 (print w2/1099 forms) '
system (exp=d)
;
define m00:'rv582 (print w2 / 1099 forms) 01/09/90'
;
;notes:
;     10-25-76...prototype work began...klf
;     10-28-76...initial release (zpr680)...klf
;     12-06-76...revised for "qualified plan" question...klf
;     04-12-77...prototype conversion of pr680 to rp582...klf
;     11-23-77...initial release (rp582)...klf
;     12-28-77...corrected city/state wage limiting...klf
;
;     12-02-78...mod for new style w-2's...jwb
;     12-27-78...corrected final sub-total print..jwb
;     12-29-78...corrected meals being added to fica wages...jwb
;     01-23-79...removed one line feed in 1099 print routine...jwb
;     08-10-79...mod for new style w-2's containing advance eic payment
;                data fields...ok
;     08-31-79...modified for vsi type master file, newer w-2's
;                with mixed english and metric measurements...ok
;     09-20-79...added jwb's form alignment option,correct format for style,
;                replace 1099-misc with 1099-nec, made tiny improvement to
;                alignment print...ok
;     10-08-79...removed vsi to make program compatible with released version...ok
;     11-15-79...modified program to incorporate the format and form type
;                option for the 2 wide w-2's...rle
;     12-20-79...modified program to print 2 styles of single wide forms and
;                added changes to allow for proper totaling when re-starting,
;                corrected vertical spacing on single wide forms...jwb
;     01-22-80...mod for more correct calculation of fica wages, corrected typo
;                after b0130 which had a "9" instead of a right parenthesis...ok
;     09-24-80...completely revised program for single style w2...jg
;     11-26-80...corrected firm address print on single wide w-2's...dle
;     01-09-81...modified for correct printing of w-2's without soc.
;                sec. number following a restart...jg
;     01-13-81...corrected to print proper tips amount in last subtotal
;                w-2 prior to grand total w-2...dle
;     01-13-81...corrected the calculation of box 13 to use fica wages
;                rather than gross wages to calculate from...dle
;   08/20/82...corrected the meal & tip flag for fica wages...pbn454...jh
;   12/09/82..bypass employees with zero wage..pbl565...jh
;    01/27/83...changed employers state number to allow 10 characters...lmv
;   02/15/83...garbage in w2-form, pbl#620---jh
;   11/16/83...modified for 1099 form changes and delete of pension block
;             print for w-2's..pl#806...dle
;   12-05-83...expand zip code print to 10 characters on w2 and clear garbage
;              out of state id# on total page..pl#835...emo
;
;   12-12-83...pbl#806...added fix to print shortfall on w2 forms...jg
;   01-03-84...expand zip code print to 10 characters on 1099..pl#853...emo
;   08-24-84...made 1099 format changes for 1984 pl#990...dle
;   01-06-84...mod.to print deficiency in box 16 of w-2's ,this shows up only if
;   employee has not had fica taken doing one or more pay periods..pl#845...rlk,jr.
;   11-30-84...pln1015..allow fwt to print if w2flag .ge. 8...rlk,jr.
;   01-11-85...made changes for box 16 (w-2) print to include only
;              unpaid fica taxes from unpaid fica field, and format
;              changes to f36a from d10,d09 to n10,n09...dle pl1024&1035
; 12/17/85...made format changes for 1985 1099's...clv ii
; 01/13/87...format changes for w2's
;   1/23/87...changing message m38 to clarify it, i hope...ns
;   1/15/88..change to print other, meals & shortfall as life, car allowance & per use of veh...ns
;   1/05/89...pulling shortfall from allocated tips field if used as vehicle allowance ..clvii
;   1/30/89...pulling car allow out of fwages for reliable...clv ii
;   7/24/89...bypass auto / benefit opts on 1099's ...clv ii
;   see b0145..clv ii
;   01/04/90...changes for 1989 w2 forms ... 1099's are ok ...clv ii
;   01/09/90...adding new option for w2 2 wide w/ envelopes...clv ii
;   01/23/90...adding tipfica(field 15) to print in box 6(allocated tips)..clvii
;   01/09/91...changes for new forms 1990..major reconstruction ...clv ii
 
;
;
;
;==============================================================================
;                      p r o g r a m   l i n k a g e
;==============================================================================
;
entrypoint crt
;
external cget,chr,clrec,f90,getkey,gupsi,hicore,ioerr,bltrun,nosign,tget
external lfeed,msg,msgn,newkey,pupsi,stat,ynget
external hldr,frer,getk,newk,getr,putr
;
;==============================================================================
;                                f i l e s
;==============================================================================
;
file crt:    sysipt
;
file prt:    syslst, class=1, seq
;
file master: sys0, class=2, ind, recsiz=435, key=mstkey
;
file tagfil: sys1, class=2, seq, filtyp=b, lsr=4, buffer=400,buf1
     buffer  buf1  (400)
;
file zipfil: sys2, class=2, random, recsiz=20, key=zipkey
 set zipkey: 0
;
file taxfil: sys3, class=2, random, recsiz=395, key=taxkey
 set taxkey: 0
;
;==============================================================================
;                               r e c o r d s
;==============================================================================
;
copy msthdr
;
;
;
copy emprec
;
record tagrec (8)
set    tag:    0
set    mstkey: 0
endrec
;
record ziprec (20)
string city   (19)
endrec
;
record taxhdr (200)
;
;      record is 395 bytes, but only first 200 are required here
;
set    taxnul: 0
define a01ref: '   '          ; tax area reference code
define a02ref: '   '
define a03ref: '   '
define a04ref: '   '
define a05ref: '   '
define a06ref: '   '
define a07ref: '   '
define a08ref: '   '
define a09ref: '   '
define a10ref: '   '
define a11ref: '   '
define a12ref: '   '
define a13ref: '   '
define a14ref: '   '
define a15ref: '   '
;
define a01nam: '        '     ; tax area name
define a02nam: '        '
define a03nam: '        '
define a04nam: '        '
define a05nam: '        '
define a06nam: '        '
define a07nam: '        '
define a08nam: '        '
define a09nam: '        '
define a10nam: '        '
define a11nam: '        '
define a12nam: '        '
define a13nam: '        '
define a14nam: '        '
define a15nam: '        '
;
endrec
;
record taxrec (260)
;
;      record is 395 bytes, but only first 260 are required here
;
set    txempl:0               ; employee number
set    a01ygr:0               ; area ytd gross pay
set    a02ygr:0
set    a03ygr:0
set    a04ygr:0
set    a05ygr:0
set    a06ygr:0
set    a07ygr:0
set    a08ygr:0
set    a09ygr:0
set    a10ygr:0
set    a11ygr:0
set    a12ygr:0
set    a13ygr:0
set    a14ygr:0
set    a15ygr:0
;
set    a01pgr:0               ; area epq gross pay
set    a02pgr:0
set    a03pgr:0
set    a04pgr:0
set    a05pgr:0
set    a06pgr:0
set    a07pgr:0
set    a08pgr:0
set    a09pgr:0
set    a10pgr:0
set    a11pgr:0
set    a12pgr:0
set    a13pgr:0
set    a14pgr:0
set    a15pgr:0
;
set    a01mgr:0               ; area mtd gross pay
set    a02mgr:0
set    a03mgr:0
set    a04mgr:0
set    a05mgr:0
set    a06mgr:0
set    a07mgr:0
set    a08mgr:0
set    a09mgr:0
set    a10mgr:0
set    a11mgr:0
set    a12mgr:0
set    a13mgr:0
set    a14mgr:0
set    a15mgr:0
;
set    a01ytx:0               ; area ytd taxes paid
set    a02ytx:0
set    a03ytx:0
set    a04ytx:0
set    a05ytx:0
set    a06ytx:0
set    a07ytx:0
set    a08ytx:0
set    a09ytx:0
set    a10ytx:0
set    a11ytx:0
set    a12ytx:0
set    a13ytx:0
set    a14ytx:0
set    a15ytx:0
endrec
;
;==============================================================================
;                                l o g i c
;==============================================================================
   ;
entry
   ;
   open io (crt, master, tagfil, zipfil)
   open output prt
   ;
   readb (tagfil, tagrec)
   go to (q0800, ioerr) on status
   if (tag .ne. 0) go to q0800
   ;
a0010:
   ;
call hldr(master)
   if (status .ne. 0) go to a0010
   ;
call getr(master,msthdr)
   call stat (1)
   ;
   'passok' = 'n';                    force door closed
   ;
call putr(master,msthdr)
   call stat (2)
   ;
   ifstring (usepas .eq. n) go to a0050; skip no security
   if (paswrd .eq. 0) go to a0050
   ;
   call msg (m001);                   enter password
   read (crt, f10) sumnum
   ;
   if (sumnum .eq. paswrd) go to a0050; skip password ok
   ;
   call msg (access)
   ;
a0020:
   ;
   curp (crt, 1, 23)
call msg (beep)
   go to a0020
   ;
a0050:
   ;
   'statax'='null'    ;pl#835
   taxflg=0           ;pl#835
   call gupsi (taxopt);               upsi flag for tax file
   ;
   if (taxopt .eq. 0) go to a0090;    skip no tax file
   ;
   open io taxfil
   ;
   call getkey (taxfil, zero)
   go to (q0700, ioerr) on status
   ;
   readb (taxfil, taxhdr)
   call stat (1)
   ;
a0090:
   ;
   call edit (fedtax, ftxbuf, mask01)
   direct
   lda/ mtop
   sta/ mltpop+2
   cpl
   ;
a0100:
   ;
   call msg (eject)
   call msg (m00)
   call msg (null)
   ;
   write (crt, f01) m101, firm;       show the colors
   write (crt, f01) m102, faddr
   write (crt, f01) m103, fcity
   ;
   call lfeed (crt, 2)
   ;
   call msg (m01);                    display option list
   write (crt, f02) m02
   write (crt, f02) m03
   write (crt, f02) m09
   ;
a0110:
   ;
   read (crt, f10) option
   if (option .eq. 0) go to a0100;    0 = redisplay screen
   if (option .eq. 9) go to q0900;    9 = end of program
   go to (a0115, a0115,  a0120) on option
   ;
   call msg (m10);                    else error
   go to a0110;                       try again
;
a0115:
   ;
if(option.ne.2)goto a0120
call msg(m157) ; standard w2 2-wide
write(crt,f02)m157a; optional reliable production w2 2-wide ( w/ envelopes)
read(crt,fn2)twowide
if(twowide.lt.1)goto a0115
if(twowide.gt.2)goto a0115
a0120:
   ;
   call ynget (m30);                  restart?
   if (status .eq. 0) go to a0130;    skip if not
   ;
   call cget (m31, f03, lempl);       enter last employee printed (restart-1)
   ;
a0130:
   ;
   if (taxopt .eq. 0) go to a0209;    bypass non-federal setup on single-state
   ;pl#835
   go to (a0200, a0200, a0910) on option
   ;
a0200:
   ;
taxflg=0 ; presume no state or city taxes
   'statax' = 'null'           ;pbln#620....jg
   'refcod'='null'   ;pl#835
   call ynget (m11);                  are state taxes being reported?
   if (status .eq. 0) go to a0210;    skip if no state
   ;
taxflg=1 ; set flag to print state tax fields
   ;
   call cget (m12, fc2, refcod);      else get state reference code
   ;
   call valtax;                       validate and setup state index
   if (sumnum .lt. 0) go to a0200;    skip unvalid reference
   ;
   'staref' = 'refcod';               save ref for m/t home check
staind=sumnum;  save state index
   call tget (stanam, staind, a01nam, 9); get state name to print
   ;
   call cget (m13, f11, statax);      state id
   ;
stamax=nines; presuppose all reportable
   call ynget (m15);                  are they?
   go to (a0205) on status
   call cget (m16, f13, stamax)
   ;
a0205:
   ;
   if (mltpop .eq. 0) go to a0210;    skip if no meals/tips pl#835
   ;
stmflg=sttflg=0;    preset to no.
call ynget(m17) ;  report state meals?
if(status.ne.0) stmflg=1 ; set to yes
   ;
   call ynget (m18);                   report state tips?
if(status.ne.0) sttflg=1 ; set to yes
   goto a0210     ;pl#835
   ;
a0209:
   'statax'='null'
   goto a0900      ;pl#835  both lines added
   ;
a0210:
   ;
   ;
   call ynget (m21);                  are city taxes being reported?
   if (status .eq. 0) go to a0900;    skip if no city
   ;
   call cget (m22, fc3, refcod);      get city reference code
   ;
   call valtax;                       validate and setup city reference code
   if (sumnum .lt. 0) go to a0210;    skip unvalid ref code
   ;
   'citref' = 'refcod';               save ref for m/t home check
citind=sumnum;   save city index
   call tget (citnam, citind, a01nam, 9); get city name to print
   ;
   call cget (m23, f11, cittax);      city id
   ;
citmax=nines ; presuppose all taxable
   call ynget (m15)
   go to (a0215) on status
   call cget (m16, f13, citmax)
   ;
a0215:
   ;
   if (mltpop .eq. 0) go to a0219;    skip if no meals/tips
ctmflg=cttflg=0 ;  preset to no.
   ;
   call ynget (m17);                   report city meals?
if(status.ne.0)ctmflg=1 ; set to yes
   ;
   call ynget (m18);                   report city tip?
if(status.ne.0)cttflg=1 ; set to yes
   ;
a0219:
   ;
   taxflg = taxflg + 2;               now set flag for city work
   ;
   ;
a0900:
   ;
   'plan'='null'                           ;qualified pension plan
   ;
a0910:
   ;
   call ynget (m37);                  plus to net also?
   direct
   lda/ status+2
   sta/ pnopt+2
   cpl
   ;
   if (taxopt .eq. 0) go to a0920;    skip if single-state
   ;
   call ynget (m38);                  print for federal reporting?
   ;
   direct
   lda/ status+2
   sta/ prtall+2
   cpl
   ;
a0920:
if(option.eq.3)goto a0930a ; added 7-24-89...clv ii - bypass on 1099's
prtauto=1;    flag print auto,life & fringe benefits
call ynget(m60);  'do other, meals, & shortfall contain benefits (print in box 16 & 17)? (y/n)')
;   note assumes that shortfall was added into gross before this program
go to (a0930)on status
 
prtauto=0
'calw'='null'
'life'='null'
'veh'='null'
'perveh'='null'
 
a0930:
if(prtauto.ne.1) goto a0930a ;  clvii
call ynget(m60a); add shortfall to box 10? (wages & comp)...clv ii
goto(a0930a) on status ; yes!
addshfl=0 ; no!
;
a0930a:
;  09-14-79...all the code from this point until a9000 added on...ok
   ;
   call ynget (m50);                  print alignnment guide?
   if (status .eq. 0) go to a8000
   count = 0
   go to (a1000, a2000, a3000) on option
   ;
a1000:
   ;
   ;
a1100:                              ;alignment guide for 1 wide w2's
   call lfeed(prt,5)
write(prt,f32a)xes,x,null,x
write(prt,f31a)xes
write(prt,f35a)xes,nines,nines
call lfeed(prt,1)
write(prt,f33a)nines,nines
call lfeed(prt,1)
write(prt,f37a)xes,xes,nines,nines
call lfeed(prt,1)
write(prt,f37a)xes,xes,nines,nines
call lfeed(prt,1)
write(prt,f35a)xes,nines,nines
call lfeed(prt,1)
write(prt,f35a)xes,nines,nines
write(prt,f35a)xes,nines,nines
write(prt,f35a)xes,nines,nines
call lfeed(prt,4)
write(prt,f36a)nines,nines,xes,nines,nines,xes
call lfeed(prt,1)
write(prt,f36a)nines,nines,xes,nines,nines,xes
call lfeed(prt,6)
   incr count
   if (count .lt. 2) go to a1100
   count = 0
   call ynget (m52);                  again?
   if (status .eq. 0) go to a8000
   go to a1100
   ;
   ;
a2000:
   ;
a2010:    ;alignment guide for 2 wide w2
goto(a2010a , a7000) on twowide ; new option for reliable
;
a2010a:
   call lfeed(prt,4)
   write(prt,f30b)xes,xes,xes,xes,xes,xes
   write(prt,f31b)xes,xes
   write(prt,f32b)xes,x,x,xes,x,x
   call lfeed(prt,1)
   write(prt,f33b1)nines,nines,nines,nines;     12/12/83...pbl806..jg
   call lfeed(prt,1)
   write(prt,f34b)xes,nines,nines,nines,xes,nines,nines,nines
   call lfeed(prt,1)
   write(prt,f35b)xes,nines,nines,xes,nines,nines
   call lfeed(prt,1)
   write(prt,f31bb)xes,nines,xes,xes,nines,xes
   write(prt,f31a)xes,calw,veh, xes,calw,veh
  call lfeed(prt,1)
   write(prt,f36b)nines,nines,xes,nines,nines,xes
   call lfeed(prt,1)
   write(prt,f36b)nines,nines,xes,nines,nines,xes
   call lfeed(prt,2); chng 3 to 2 for 1986 format ...clv ii
goto a7999 ; check for 3 times
;=============
; two wide for reliable
;
a7000:
;
call lfeed(prt,2)
write(prt,f30bb)xes,xes
call lfeed(prt,1)
write(prt,f30r)xes,xes,xes,xes
write(prt,f31b)xes,xes
write(prt,f32r)xes,x,x,xes,x,x
call lfeed(prt,1)
write(prt,f33r1)nines,nines,nines,nines
call lfeed(prt,1)
write(prt,f34r)xes,nines,nines,nines,xes,nines,nines,nines
call lfeed(prt,1)
write(prt,f35r)xes,nines,nines,xes,nines,nines
call lfeed(prt,1)
write(prt,f31rr)xes,nines,xes,xes,nines,xes
write(prt,f31b)xes,xes
call lfeed(prt,1)
write(prt,f36r)nines,nines,xes,nines,nines,xes
call lfeed(prt,1)
write(prt,f36r)nines,nines,xes,nines,nines,xes
call lfeed(prt,2)
;
a7999:
;
incr count
if(count.lt.3)goto a2010
   count = 0
   call ynget (m52);                  again?
   if (status .eq. 0) go to a8000
   go to a2010
   ;
   ;
   ;
a3000:
   call lfeed(prt,4)
   write(prt,f41)xes
   write(prt,f41)xes
   write(prt,f41)xes
   call lfeed(prt,2)
   write(prt,f41c)testfed,testss,nines
   call lfeed(prt,1)
   write(prt,f41)xes
   call lfeed(prt,2)
   write(prt,f41b)xes,nines
   call lfeed(prt,1)
   write(prt,f41)xes
   call lfeed(prt,5)
   ;
;
   call ynget (m52);                  again?
   if (status .eq. 1) go to a3000;    let's have another
a8000:
   'zeropt'='n'                    ;12/09/82...jh
   call ynget(m53)                 ;print zero balances ...12/09/82...jh
   if(status .eq.0)go to a9000     ;this means no...jh
   'zeropt'='y'                    ;this means yes...jh
   ;
a9000:
   ;
   call ynget (m39);                  ready to print?
   if (status .eq. 0) go to a0100;    0 = no = restart screen
   ;
;==============================================================================
;            i n p u t   a n d   w - 2   c a l c u l a t i o n
;==============================================================================
   ;
b0100:
   ;
   readb (tagfil, tagrec)
   go to (d0110, ioerr) on status
   if (tag. le. 0) goto b0100 ; 12-02-78...jwb
   ;
call getr(master,emprec)
   call stat (1)
   gropay=0          ;let's check for zero balance 12/09/82..jh
   ifstring(zeropt .eq. y) go to b0105   ;print zero bal so go around..jh
   gropay=fwt+gross+tips+fica              ;is there hidden money?...jh
   if(gropay .eq.0) go to b0100          ;this is zero check nxt guy...jh
b0105:
   ;
   call edit (ssno, ssnum, mask02);   set empl id
   ;
   direct
   lda/ w2s
   sta/ w2flag+2
   cpl
   ;
   'city' = 'null'
   call getkey (zipfil, zipcod)
   go to (b0110, ioerr) on status
   ;
   readb (zipfil, ziprec)
   call stat (1)
   ;
b0110:
   ;
   if (option .ge. 3) go to c0100;    skip if printing 1099's
   ;
   if (w2flag .ge. 8) go to b0100; pln1015    loop if empl doesn't get w2
   ;
   if (pass. ne. 0) goto b0115 ; 12-02-78...jwb
   if (ssno. ne. 0) goto b0117 ; 12-02-78...jwb
   nossno=nossno+1 ; 12-02-78...jwb
   goto b0100 ; 12-02-78...jwb
   ;
b0115: ; 12-02-78...jwb
   ;
   if (ssno. eq. 0) goto b0117 ; 12-02-78...jwb
   goto b0100 ; 12-02-78...jwb
   ;
b0117: ; 12-02-78...jwb
   ;
cwages=swages=0 ; set to zero
   if (taxopt .eq. 0) go to b0130
   if (taxflg .eq. 0) go to b0130
   ;
   call getkey (taxfil, empl)
   go to (q0800, ioerr) on status
   ;
   readb (taxfil, taxrec)
   call stat (1)
   ;
   if (taxflg .eq. 2) go to b0120
   ;
   call tget (swages, staind, a01ygr, 4); get ytd state wages
   call tget (axxswt, staind, a01ytx, 4); get ytd state taxes
   if(prtauto.ne.1)go to b0118;   1/89
   if(swages.eq.0)go to b0118; 1/89
   swages=swages+meals+other;   life/car/car allowance add into state gross too. (car already added)
   ;
 
b0118:;  1/89
   go to (b0130)on taxflg;  only state
   ;
b0120:
   ;
   call tget (cwages, citind, a01ygr, 4); get ytd city wages
   call tget (axxcwt, citind, a01ytx, 4); get ytd city taxes
   ;
b0130:
   ;
wages=gross ; calculate total annual wages
fwages=0 ; presume no taxable wages
   ;
   if (mltpop .eq. 0) go to b0140;    skip if meals/tips not in effect
   wages = gross + tips
   ;
   if (taxopt .eq. 0) go to b0135;    skip non-federals on single-state
   if (cttflg .eq. 0) go to b0133;    skip not report city tips
   ;
   ifstring (citcod .ne. citref) go to b0133
   cwages = cwages + tips
   ;
b0133:
   ;
   if (sttflg .eq. 0) go to b0135;    skip not report state tips
   ifstring (stacod .ne. staref) go to b0135
   swages = swages + tips
   ;
b0135:
   ;
   go to (b0140)on prtauto
   if (mltpop .eq. 2) go to b0140;    skip if no taxable meals
   wages = wages + meals
   ;
   if (taxopt .eq. 0) go to b0140;    skip non-federals on single-state
   if (ctmflg .eq. 0) go to b0137;    skip not report city meals
   ;
   ifstring (citcod .ne. citref) go to b0137
   cwages = cwages + meals
   ;
b0137:
   ;
   if (stmflg .eq. 0) go to b0140;    skip not report state meals
   ifstring (stacod .ne. staref) go to b0140
   swages = swages + meals
   ;
b0140:
   ;
fwages=yfwage; calculate fica taxable annual wages
   go to (b0145)on prtauto
   if (mltpop. eq. 2) goto b0146 ; don't add meals to fica wages ...pbn454..jh
   fwages=fwages+meals ; add em in
  go to b0146
 
b0145:; 1/89
   ;  have car allowance etc
   fwages=fwages+other;   shortfall already in gross;  pulled meals 1/30/88..clvii
   ;1/89
 
   ;
b0146: ; 01-22-80...added next two paragraphs of code (b0145 until b0145b)...ok
   ;
   if (mltpop .eq. 0) go to b0147
   if (fwages .ge. ficmax) go to b0146a
   sumnum = fwages + tips           ; see how much tips affect total
   if (sumnum .le. ficmax) go to b0150 ; leave all numbers alone, they're fine
   tips = ficmax - fwages           ; calculate maximum reportable tips
   go to b0150
   ;
b0146a:
   ;
tips=zero ; no reportable fica tips because fwages reached ficmax
   ;
b0147:
   ;
   if (fwages .le. ficmax) go to b0150
fwages=ficmax
   ;
b0150:
   ;
   if (pnopt .eq. 0) go to b0160;     skip to hide plus-to-net       (12-28-77)
   ;
   wages = wages + ypl2nt;            else add to ytd wages
   ;
   if (taxopt .eq. 0) go to b0190
   ifstring (citcod .ne. citref) go to b0155; skip not home city
   ;
   cwages = cwages + ypl2nt
   ;
b0155:
   ;
   ifstring (stacod .ne. staref) go to b0160; skip not home state
   ;
   swages = swages + ypl2nt
   ;
b0160:
   ;
   if (taxopt .eq. 0) go to b0190;    no wage maxing if single state (12-28-77)
   ;
   if (cwages .le. citmax) go to b0165
cwages=citmax
   ;
b0165:
   ;
   if (swages .le. stamax) go to b0190
swages=stamax
   ;
b0190:
 
   if(prtauto.ne.1)go to b0195
   wages=wages+other+meals;  liberty already added shortfall ...
if(addshfl.ne.1)goto noadd; ...clvii
;
addit:
   wages=wages+shortfall ;
noadd:
 
b0195:
 
   ;
   if (taxopt .eq. 0) go to b0200;    single-state does all
   if (prtall .eq. 1) go to b0200;    multi-area ordered to print all
   ;
   if (swages .gt. 0) go to b0200;    multi-area supplemental print
   if (cwages .gt. 0) go to b0200
   if (axxswt .gt. 0) go to b0200
   if (axxcwt .le. 0) go to b0100;    no print supplemental if city/state $0.00
   ;
b0200:
   ;
   if (empl .le. lempl) call update;  total up to restart
   if (empl. le. lempl) goto b0100
   totemp=totemp+1
   ?fwt1=?fwt1+fwt
   ?wage1=?wage1+wages
   ?fica1=?fica1+fica
   ?fwag1=?fwag1+fwages
   ?eic1=?eic1+yeic
   ;
   go to (b0205, b0305) on option
   ;
;==============================================================================
;                   p r i n t   o n e - w i d e   w - 2
;==============================================================================
   ;
;
b0205:
   ;
call lfeed(prt,5)
if(retfnd.gt.0)write(prt,f32a)firm,null,x
if(retfnd.le.0)write(prt,f31a)firm
b0210:
   write(prt,f31a)faddr
if(prtauto.eq.1)write(prt,f35a)fcity,zero,yeic
if(prtauto.eq.0)write(prt,f35a)fcity,shortfall,yeic
call lfeed(prt,1)
write(prt,f33a)fwt,wages
call lfeed(prt,1)
if(taxflg.eq.2)write(prt,f37a)[ftxbuf+1],null,fica,fwages
if(taxflg.ne.2)write(prt,f37a)[ftxbuf+1],statax,fica,fwages
call lfeed(prt,1)
   ?fwt=?fwt+fwt
   ?wages=?wages+wages
   ?fica=?fica+fica
   ?fwage=?fwage+fwages
   ?eic=?eic+yeic
if(prtauto.eq.1)goto b0238a;
?shrtfall=?shrtfall+shortfall
?shrt = ?shrt + shortfall;                  12/12/83...pbl#806..jg
b0238a:
call zipfix
   if(fwages.ge.ficmax)tips=0        ;don't report tips over maximum
write(prt,f37a)[ssnum+1],null,tips
call lfeed(prt,1)
if(prtauto.eq.1) call setlife; puts life,c-alw, veh into fields
write(prt,f35ab)name,zero,field4
call lfeed(prt,2)
if(retfnd.gt.0)write(prt,f35aa)addr,'d-',retfnd,field1
else write(prt,f35a)addr
if(prtauto.eq.1)write(prt,f35aab)city,[zipbuf+1],zero,field3
else write(prt,f31c)city,[zipbuf+1]
call lfeed(prt,4)
   ?tips=?tips+tips
   ?tips1=?tips1+tips
   ?tips2=?tips2+tips
b0250:
; b0250 added for pln#845...rlk,jr.
;if(nopafica.le.0)goto b0250a
;call lfeed(prt,2);chng 1 to 2 12/29/86..clvii
;write(prt,f31aa)addr,nopafica,npmes
;?tdef=?tdef+nopafica
;?fdef=?fdef+nopafica
;goto b0250d
b0250a:
b0250d:
 
b0260:
   write(prt,f36a)axxswt,swages,stanam,axxcwt,cwages,citnam
   ?swt1=?swt1+axxswt
   ?swag1=?swag1+swages
   ?swt2=?swt2+axxswt
   ?swag2=?swag2+swages
   ?cwt1=?cwt1+axxcwt
   ?cwag1=?cwag1+cwages
   ?cwt2=?cwt2+axxcwt
   ?cwag2=?cwag2+cwages
b0290:
   call lfeed(prt,8)         ;skip to top of form;12/29/86 3 to 2 ..clvii
;
b0300:
   sempl=sempl+1
   templ=templ+1
   if(sempl.lt.41)goto b0100
   call lfeed(prt,5)
write(prt,f32a)firm,x
write(prt,f31a)faddr
if(prtauto.eq.0)write(prt,f35t)fcity,?shrtfall,?eic
else write(prt,f35t)fcity,zero,?eic
call lfeed(prt,1)
write(prt,f34t)?fwt,?wages
call lfeed(prt,1)
write(prt,f37t)[ftxbuf+1],null,?fica,?fwage
call lfeed(prt,1)
write(prt,f37t)null,null,?tips
call lfeed(prt,1)
if(prtauto.eq.1)write(prt,f35a)null,zero,?shrtfall
else call lfeed(prt,1)
call lfeed(prt,17)
   sempl=0
   ?fwt=?wages=?fica=?fwage=?tips=?eic=0
?shrtfall = 0 ;                              12/12/83...pbl#806..jg
   goto b0100
;
;          ===========================================
;            print two wide w2's
;          ===========================================
;
b0305:
   goto(b0310,b0320,b0310)on taxflg
   goto b0320
;
b0310:
if(twowide.eq.2)goto b310a
   call lfeed(prt,4)
   write(prt,f30b)firm,[ftxbuf+1],statax,firm,[ftxbuf+1],statax
   goto b0330
b310a:
  call lfeed(prt,2)
  write(prt,f30bb)[ftxbuf+1],statax
  call lfeed(prt,1)
  write(prt,f30r)firm,firm,[ftxbuf+1],statax
    goto b0330
;
b0320:
if(twowide.eq.2)goto b320a
  call lfeed(prt,4)
   write(prt,f30b)firm,[ftxbuf+1],null,firm,[ftxbuf+1],null
;
  goto b0330
;
b320a:
  call lfeed(prt,2)
  write(prt,f30bb)[ftxbuf+1],null
  call lfeed(prt,1)
  write(prt,f30r)firm,firm,[ftxbuf+1],null
;
b0330:
   write(prt,f31b)faddr,faddr
if(twowide.eq.2) -
   write(prt,f32r)fcity,null,null,fcity,null,null
   else write(prt,f32b)fcity,null,null,fcity,null,null
   call lfeed(prt,1)
if (shortfall.ne.0) go to b0335;            12/12/83...pbl#806..jg
if(twowide.eq.2) -
   write(prt,f33r1)tipfica,yeic,tipfica,yeic  ; tipfica prints in box 6 ..clv ii
else write(prt,f33b)yeic,yeic
go to b0338;                               12/12/83...pbl#806..jg
;
;==============
b0335:;                                     12/12/83...pbl#806..jg
;==============
;
if(prtauto.eq.1)goto b0335a
if(twowide.eq.2) -
   write(prt,f33r1)shortfall,yeic,shortfall,yeic
   else write(prt,f33b1)shortfall,yeic,shortfall,yeic
goto b0335b
;
b0335a:
if(twowide.eq.2) -
write(prt,f33r1)tipfica,yeic,tipfica,yeic
else write(prt,f33b)yeic,yeic
;
b0335b:
;
;
;==============
b0338:
;==============
;
   call lfeed(prt,1)
if(twowide.eq.2) -
   write(prt,f34r)[ssnum+1],fwt,wages,fica,[ssnum+1],fwt,wages,fica
   else write(prt,f34b)[ssnum+1],fwt,wages,fica,[ssnum+1],fwt,wages,fica
   call lfeed(prt,1)
   ?fwt=?fwt+fwt
   ?wages=?wages+wages
   ?fica=?fica+fica
   ?fwage=?fwage+fwages
   ?eic=?eic+yeic
if(prtauto.eq.1)goto b0338a
?shrtfall = ?shrtfall + shortfall;
?shrt = ?shrt + shortfall;                  12/12/83..pbl#806..jg
b0338a:
   if(fwages.ge.ficmax)goto b0340         ;don't report tips over maximum
if(twowide.eq.2) -
   write(prt,f35r)name,fwages,tips,name,fwages,tips
   else write(prt,f35b)name,fwages,tips,name,fwages,tips
   ?tips=?tips+tips
   ?tips1=?tips1+tips
   ?tips2=?tips2+tips
   goto b0350
;
b0340:
if(twowide.eq.2) -
   write(prt,f35r)name,fwages,zero,name,fwages,zero
   else write(prt,f35b)name,fwages,zero,name,fwages,zero
;
b0350:
; b0350 added for pln#845...rlk,jr.
if(nopafica.le.0)goto b0350a
call lfeed(prt,1)
if(twowide.ne.2) call lfeed(prt,1) ;  need 2 lines if not for reliable..clv ii
if(twowide.eq.2) -
  write(prt,f31rr)addr,nopafica,npmes,addr,nopafica,npmes
  else write(prt,f31rr)addr,nopafica,npmes,addr,nopafica,npmes
call lfeed(prt,1)
;
b350a:
?tdef=?tdef+nopafica
?fdef=?fdef+nopafica
goto b0350d
b0350a:
 
   go to (b0350c)on prtauto
  call lfeed(prt,1)
  if(twowide.ne.2) call lfeed(prt,1) ; need 2 lines if not for reliable..clvii
   write(prt,f31b)addr,addr
if(twowide.eq.2) call lfeed(prt,1) ; it goes here for reli...clv ii
   go to b0350d
 
b0350c:
 
   call setlife
   call lfeed(prt,1)
if(twowide.eq.2)goto b350c
   write(prt,f31aaa)null,field1,field2, null,field1,field2
;   f31aaa ...1/15/88...clv ii for personal use of vehicle.
   write(prt,f31aaa)addr,field3,field4, addr,field3,field4
    goto b0350d
;
b350c:
  write(prt,f31bbb)null,field1,field2, null,field1,field2
  write(prt,f31bbb)addr,field3,field4, addr,field3,field4
;
 
b0350d:
   call zipfix         ;pl#835
   write(prt,f31d)city,zipbuf,city,zipbuf
   if(taxflg.eq.1)goto b0360               ;state taxes
   if(taxflg.eq.3)goto b0360               ;state taxes
   call lfeed(prt,2)
   goto b0370                              ;check local taxes
;
b0360:
if(twowide.eq.2) -
   write(prt,f36r)axxswt,swages,stanam,axxswt,swages,stanam
   else write(prt,f36b)axxswt,swages,stanam,axxswt,swages,stanam
   call lfeed(prt,1)
   ?swt1=?swt1+axxswt
   ?swag1=?swag1+swages
   ?swt2=?swt2+axxswt
   ?swag2=?swag2+swages
;
b0370:
   if(taxflg.gt.1)goto b0380               ;local taxes
   call lfeed(prt,1)                       ;skip print - no local taxes
   goto b0390                              ;position to top of form
;
b0380:
if(twowide.eq.2) -
   write(prt,f36r)axxcwt,cwages,citnam,axxcwt,cwages,citnam
   else write(prt,f36b)axxcwt,cwages,citnam,axxcwt,cwages,citnam
   ?cwt1=?cwt1+axxcwt
   ?cwag1=?cwag1+cwages
   ?cwt2=?cwt2+axxcwt
   ?cwag2=?cwag2+cwages
;
b0390:
   call lfeed(prt,2)    ;skip to top of form;  chng 3 to 2 for 1986 format ..clvii
   goto b0300                              ;check subtotal
;
;
;
;==============================================================================
;                           p r i n t   1 0 9 9
;==============================================================================
   ;
c0100:
   ;
   if (w2flag .lt. 8) go to b0100;   pln1015.rlk,jr.    loop if empl doesn't get 1099's
   ;
   if (pnopt .eq. 0) go to c0110;     skip to hide plus-to-net
   ;
   gross = gross + ypl2nt
   ;
c0110:
   ;
   if (empl .le. lempl) go to c0120;  skip to total up to restart
call lfeed (prt,4); 12/17/85...change for 1985 1099 format...clv ii
   write (prt, f41) firm
write(prt,f41)faddr
   write (prt, f41) fcity
   call lfeed (prt,2)
   write (prt, f41c) [ftxbuf+1],[ssnum+1],fwt
   call lfeed (prt, 1)
   write (prt, f41) name
   call lfeed (prt, 2)
   write (prt,f41b) addr,gross
   call lfeed(prt,1)
   call zipfix         ;pl#853
   write (prt, f41a) city, [zipbuf+1]     ;pl#853
   ;
   call lfeed (prt, 5)
   ;
c0120:
   ;
   templ = templ + 1;                 incr number of 1099's printed
   ;
  ?totfwt=?totfwt+fwt
;?totfwt added for pln1015...rlk,jr.
   ?wages = ?wages + gross
   ;
   go to b0100
   ;
;==============================================================================
;                 p r i n t   t o t a l s   &   c l o s e
;==============================================================================
   ;
;
d0110:
   if(option.eq.3)goto d0270           ;1099
   if(sempl.le.0)goto e0100
;
d0130:
   if(sempl.le.2)goto d0140
   sempl=sempl-2
   goto d0130
;
;          ============================================
;             w2 subtotals
;          ============================================
;
d0140:
   goto(d0160,d0150)on sempl
;
d0150:
   call lfeed(prt,5)
write(prt,f32a)null,null,null,x
call lfeed(prt,27)
goto d0160
;
d0160:
d0170:
   call lfeed(prt,5)
d0180:
   if(templ.lt.41)goto d0180a
write(prt,f32a)firm,x
goto d0180b
;
d0180a:
write(prt,f32a)firm,null,null,x ; void
;
d0180b:
write(prt,f31a)faddr
if(prtauto.eq.0)write(prt,f35t)fcity,?shrtfall,?eic
else write(prt,f35t)fcity,zero,?eic
   call lfeed(prt,1)
write(prt,f34t)?fwt,?wages
   call lfeed(prt,1)
write(prt,f37t)[ftxbuf+1],statax,?fica,?fwage
   call lfeed(prt,1)
   write(prt,f37t)null,null,?tips
   call lfeed(prt,4)
   ;write(prt,f37t)?tdef,npmes,templ
   ;write(prt,f37ta)life,tb1,calw,tb2,veh,tb3
write(prt,f10)templ
call lfeed(prt,5)
   write(prt,f36t)?swt2,?swag2,stanam,?cwt2,?cwag2,citnam
   call lfeed(prt,8)
 ?tdef=0
goto e0100
;
;          ===================================
;             1099 total
;          ===================================
   ;
   ;
d0270:
   ;
   call lfeed (prt,3); 09-20-79...mod for 1099-nec...ok
   write (prt, f44) totmsg, templ
   call lfeed (prt, 4)
  write(prt,f45)?totfwt      ;pln1015
call lfeed(prt,3)     ;pln1015
write(prt,f45) ?wages
   call lfeed (prt, 8)
   goto q0909
   ;
;          ==============================================
;           check for missing s.s. numbers
;          ==============================================
;
e0100:
   ;
   ; write (prt, f90) eject ; 12/29/78...jwb
   if (nossno. eq. 0) goto e0200 ; 12-02-78...jwb
   pass=1 ; 12-02-78...jwb
   templ=sempl=nossno=lempl=0 ; 01-09-81...jg
   tb1=tb2=tb3=?fwt=?wages=?fica=?fwage=?tips=?eic=0 ; 12-02-78...jwb, 08-10-79...ok
?shrtfall = 0;                           12/12/83...pbl#806..jg
   ?tips2=?cwt2=?cwag2=?swt2=?swag2=0
   close prt ; 12-02-78...jwb
   open output prt ; 12-02-78...jwb
   rewind tagfil ; 12-02-78...jwb
   call msg (null) ; 12-02-78...jwb
   call msg (m40)  ; 12-02-78...jwb
   call msg (m41)  ; 12-02-78...jwb
   call msg (m42)  ; 12-02-78...jwb
   read (crt, f11) ssnum ; 12-02-78...jwb
   goto b0100 ; 12-02-78...jwb
   ;
e0200: ; 12-02-78...jwb
   ;
   call getk (master, zero)
   if (status .ne. 0) call ioerr
   ;
   'yrdone' = 'y'
   ;
call putr(master,msthdr)
   call stat (2)
   ;
   go to q0900
   ;
q0700:
   ;
   call msg (m9700)
   go to q0910
   ;
q0800:
   ;
   call msg (m9800);                  invalid master file
   go to q0910
   ;
q0900:
   ;
   if (pass. ne. 0) goto q0905a
   if (totemp. gt. 41) goto q0905a
   goto q0909
   ;
;          ======================================
;            print final w2 totals
;          ======================================
;
q0905a:
   write(prt,f50)m28                       ;final totals
   call lfeed(prt,4)
write(prt,f32a)firm,x
write(prt,f31a)faddr
if(prtauto.eq.0)write(prt,f35t)fcity,?shrt,?eic1
else write(prt,f35t)fcity,zero,?eic1
call lfeed(prt,1)
write(prt,f34t)?fwt1,?wage1
call lfeed(prt,1)
write(prt,f37t)[ftxbuf+1],null,?fica1,?fwag1
call lfeed(prt,1)
write(prt,f37t)null,null,?tips1
call lfeed(prt,4)
write(prt,f10)totemp
call lfeed(prt,5)
write(prt,f36t)?swt1,?swag1,stanam,?cwt1,?cwag1,citnam
call lfeed (prt,8)
q0909:
   ;
   write (prt, f90) eject
   call msg (m9900);                  end of program
   ;
q0910:
   ;
   close crt, prt, master
   stop 0
   ;
;==============================================================================
;                          s u b r o u t i n e s
;==============================================================================
;
;=========================
;
;=========================
;=========================
;=========================
;=========================
subroutine setlife
;========================
'field1'='null'
'field2'='null'
'field3'='null'
'field4'='null'
if(other.eq.0)goto sl10
'field1'='life'
encode(field1,fn162)other
tb1=tb1+other
fb1=fb1+other
 
sl10:
 
if(meals.eq.0)go to sl20
'field3'='calw'
encode(field3,fn162)meals
tb2=tb2+meals
fb2=fb2+meals
 
sl20:
 
if(shortfall.eq.0)go to sl90
'field2'='perveh'
'field4'='veh'
encode(field4,fn162)shortfall
tb3=tb3+shortfall
fb3=fb3+shortfall
 
sl90:
 
return
 
;===========================
subroutine update
;===========================
;
?fwt=?fwt+fwt
?eic=?eic+yeic
?wages=?wages+wages
?fica=?fica+fica
?fwage=?fwage+fwages
?fwt1=?fwt1+fwt
?wage1=?wage1+wages
?fica1=?fica1+fica
?fwag1=?fwag1+fwages
?eic=?eic+yeic
;
if (fwages. ge. ficmax) goto up1
?tips=?tips+tips
?tips1=?tips1+tips
?tips2=?tips2+tips
;
up1:
;
?swt1=?swt1+axxswt
?swag1=?swag1+swages
?swt2=?swt2+axxswt
?swag2=?swag2+swages
?cwt1=?cwt1+axxcwt
?cwag1=?cwag1+cwages
?cwt2=?cwt2+axxcwt
?cwag2=?cwag2+cwages
;
totemp=totemp+1
sempl=sempl+1
templ=templ+1
if (sempl. lt. 41) goto up99
sempl=0
?eic=?fwt=?wages=?fica=?fwage=?tips=0
;
up99:
;
return
;
;=========================
subroutine valtax
;=========================
;
call nosign (refcod)
ifstring (refcod .eq. null) go to valter
;
sumnum=-1;
;
valt1:
incr sumnum
if (sumnum .ge. 15) go to valter;      error on undefined area
call tget (str3, sumnum, a01ref, 4); fetch next area ref code
call bltrun (str3)
ifstring (str3 .eq. refcod) go to valtx
go to valt1
;
valter:
call msg (invref)
call cget (acknow, fc1, refcod);      newline to acknowledge
sumnum=-1;
;
valtx:
return
;
;============================================================================
                         subroutine zipfix
;============================================================================
;
zp100:
  'zipbuf'='blk10'      ;fill with 10 blanks
  if (zipcod.gt.fiv9s) goto zp110  ;9 digit zip code
  call edit (zipcod,zipbuf,zmsk1)
  return
;
zp110:
  call edit (zipcod,zipbuf,zmsk2)  ;insert hyphen
  return
;
;==============================================================================
;            f o r m a t s ,  m e s s a g e s ,  &  s t o r a g e
;==============================================================================
;
define blk10:'          '
define life:'life            '
define calw:'c-alw           '
define perveh:'personal use of'
define veh :'veh.            '
define field1:'                '
define field2:'                '
define field3:'                '
define field4:'                '
define testfed:'99-9999999'
define testss:'999-99-9999'
define zmsk1:'0 @#####'
define zmsk2:'0 @#####-####'
string zipbuf(10)
set fiv9s:99999
set addshfl:1 ; add shortfall to wages ...clv ii 1/4/89
set ?fdef:0,?def:0, ?tdef:0
set ?fic:0
set ten:100000,fiv:50000
define npmes:'unpaid fica'
define access:'***   unauthorized access attempt   ***'
define acknow:'enter "newline" to acknowledge'
define invref:'***   undefined reference code   ***'
define n:'n'
define totmsg:'**  total forms printed'
define y:'y'
define x: 'x'
;
define mask01:'0 @##-#######'
define mask02:'0 @###-##-####'
;
define m001:'enter password'
;
define m01:'enter option:  1  print form w-2'
define m02:'2  print two-wide form w-2'
define m03:'3  print form 1099-misc'
define m09:'9  end program'
define m10:'invalid - enter option'
;
define m157:'enter option:  1  standard w2 2-wide'
define m157a:'2  optional w2 2-wide with envelopes'
define m11:'are state taxes being reported? (y/n)'
define m12:'enter state reference code'
define m13:'enter state reporting id'
define m15:'are all wages to be reported?  (y/n)'
define m16:'enter maximum wage to be reported'
define m17:'are meals'
define m18:'are tips'
define m19:' to be reported as wages? (y/n)'
;
define m21:'are city  taxes being reported? (y/n)'
define m22:'enter city reference code'
define m23:'enter city reporting id'
define m28:'grand totals - for employers use only - do not file'
;
define m30:'is this a restart of printing?'
define m31:'enter number of last employee printed'
define m33:'were employees covered by a qualified pension plan? (yes/no)'
define m37:'report "plus-to-net" also? (y/n)'
define m38:'should all w2s print even if state amount is zero?  (y/n)'
define m39:'ready to print?  (y/n)'
;
define m40:'master file contains employee(s) without a social security number'
define m41:'align w-2 forms at the beginning of a new page and then newline'
define m42:'to print those forms'
;
define m50: 'print alignment guide? (y/n)'
define m52: 'print another alignment guide? (y/n)'
define m53: 'print employees with zero wage balance? (y/n)'
define m60:'do other, meals, and shortfall contain benefits for box 16 & 17? (y/n)'
define m60a:'do you want to add shortfall (vehicle allowance) to box 10? (y/n)'
;
define m101:'firm    :'
define m102:'address :'
define m103:'cit sta :'
;
define m9700:'*   invalid tax file'
define m9800:'*   invalid master file'
define m9900:'end of program'
define xes:'xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx'
;
format f01: c10, c50
format fn162: n-16.2
format f02: x15, c50
format f03: n6
format f10: n10
format f11: c10
format f13: n10.2
format f1000:n13 n8 d13 d13 n13
   ;
;          ===============================================
;            formats for 1 wide w2's
;          ===============================================
;
format f30a:c30,x3,c10,x7,c12                     ;line 5
format f31a:c30 x3 c15 c16 x4            ;lines 6,15
format f31aaa: c30 x3 c15 c16 x4 c30 x2 c15 c16 ; for w2 2 wide / pers. u of veh
;format f31aa:c30 n11.2,x1,c11
format f31aa:c30 x7 n11.2 x3 c11
format f31c:c20,x1,c11
format f32a:c30,x23,c1,x3 c1 x3,c1               ;line 7
;format f33a:x50,n11.2                             ;line 9
format f33a:x36 n11.2 x4 n11.2
format f33a1:x33,n11.2,x6,n11.2                  ;line 9 with shortfall
format f33a1a: x50 n11.2                        ; line w/o shtfll - vehicle allowance...clv ii
format f34a:c11,x5,n11.2,x6,n11.2,x6,n11.2        ;line 11
;format f35a:c30,x3,n11.2,x6,n11.2                 ;line 13
format f35a:c30 x6 n11.2 x4 n11.2
format f35ab: c30 x6 n11.2 c16
format f35aa:c30 x3 c3 n11.2 c16
format f35aab:c20 x1 c11 x4 n11.2 c16
;format f36a:x32,n10.2,x1,n9.2,x1,c10              ;lines 17,19
format f36a:n9.2 n11.2 x1 c10 n11.2 n11.2 c10
format f37a:x4 c11 x5 c10 x6 n11.2 x4 n11.2
;
;          ==============================================
;            formats for 2 wide w2's
;          ==============================================
;
format f30b:c30,x3,c10,x7,c12,x6,c30,x3,c10,x7,c12
format f30r:c30 x38 c30 x3 c10 x7 c12
format f30bb:x17 c10 x3 c12
format fn2:n2
format f31b:c30,x38,c30
format f31bb:c30 n14.2 x5 c11 x8 c30 n14.2 x5 c11
format f31bbb:c30 x2 c15 c15 x6 c30 x4 c15 c15
format f31rr:c30 n14.2 x2 c7 x15 c30 x3 n11.2 x5 c11
format f31d:c20,x1,c11,x36,c20,x1,c11          ;line 16, 2 wide w2
format f32b:c30,x23,c1,x7,c1,x6,c30,x23,c1,x7,c1
format f32r: c30 x14 c1 x8 c1 x14 c30 x23 c1 x7 c1
format f33b:x50,n11.2,x57,n11.2
format f33r:x47 n11.2 x60 n11.2
format f33b1:x33,n11.2,x6,n11.2,x40,n11.2,x6,n11.2;  12/12/83..pbl#806..jg
format f33r1:x33 n11.2,x3 n11.2 x43 n11.2 x6 n11.2
format f33b1a: x50 n11.2 x57 n11.2 ; w/o shortfall - vehicle allowance...clv ii
format f34b:c11,x5,n11.2,x6,n11.2,x6,n11.2,x7,c11,x5,n11.2,x6,n11.2,x6,n11.2
format f34r:c11 x5 n11.2 x6 n11.2 x3 n11.2 x10 c11 x5 n11.2 x6 n11.2 x6 n11.2
format f35b:c30,x3,n11.2,x6,n11.2,x7,c30,x3,n11.2,x6,n11.2
format f35r:c30 x3 n11.2 x3 n11.2 x10 c30 x3 n11.2 x6 n11.2
format f36b:x32,n10.2,x2,n9.2,x1,c10,x35,n10.2,x2,n9.2,x2,c10; change for 1989...clvii
format f36r:x30 n9.2 x1 n9.2 x1 c10 x40 n10.2 x1 n9.2 x1 c10
;
;          ======================================
;           formats for w2 totals
;          ======================================
;
;format f33t:x33,d11.2,x6,d11.2;                  12/12/83...pbl#806..jg
format f33t:x50,d11.2
format f34t:x36 d11.2 x4 d11.2
format f35t:c30 x6 n11.2 x4 d11.2
format f36t:d9.2 d11.2 x1 c10 d11.2 d11.2 c10
format f37t:x4 c10 x6 c10 x6 d11.2 x4 d11.2
;format f34t:x16,d11.2,x6,d11.2,x6,d11.2
;format f35t:x33,d11.2,x6,d11.2
;format f36t:x32,d10.2,x1,d9.2,x1,c10
;format f37t:x30,d11.2 x1 c11 x1 n5
format f37ta: x23 c4 n8.2 x1c5 n8.2 x1 c4 n8.2
;
;          ==========================================
;             1099 formats
;          ==========================================
;
format f41:x2,c30
format f41a:x2,c19,c10
format f41b:x2 c30 x4 n12.2
set ?totfwt:0
format f42:x2,c10
format f41c: x2 c16 x1 c16 x15 n12.2
format f43:x2,c29,x3,c11,x4,n11.2
format f44:x2,c23,n6
format f45:x45,d12.2
format f46:x55n11.2
;
;          ===========================================
;             void w2's
;          ===========================================
;
format f50:c51
;
format fc1: c1
format fc2: c2
format fc3: c3
;
;      program work integers
;
set ?shrtfall:0
set ?shrt:0;                        total shortfall for w2's
set prtauto:0,tb1:0,tb2:0,tb3:0;   total benefits fields
set fb1:0,fb2:0,fb3:0;   final total benefits
set ?eic1  :0
set twowide:0 ; new flag for reliables 2-wide w2's with envelopes
set totemp:0
set ?fwt1  :0
set ?fica1 :0
set ?fwag1:0
set ?wage1:0
set ?tips1 :0
set ?swt1  :0
set ?swag1:0
set ?swt2  :0
set ?swag2:0
set ?cwt1  :0
set ?cwag1:0
set ?cwt2  :0
set ?cwag2:0
set ?tips2:0
set    count:  0
set    nines:  99999999
set    ?fica:  0
set    ?fwt:   0
set    ?fwage: 0
set    ?wages: 0
set    ?tips:  0
set    ?eic:   0
set    axxcwt: 0;                     city tax withheld
set    axxswt: 0;                     state tax withheld
set    citind: 0
set    citmax: 0
set    ctmflg: 0;                     flag meals reportable to city
set    cttflg: 0;                     flag tips  reportable to city
set    cwages: 0;                     city wages
set    fwages: 0;                     fica wages
set    lempl:  0;                     last employee printed (for restart)
set    mltpop: 0;                     4-byte version of meals/tips option
set    option: 0;                     run option
set    pnopt:  0;                     plus to net report option
set    prtall: 0;                     print all w-2's (even if city/state = $0.00)
set    staind: 0
set    stamax: 0
set    stmflg: 0;                     flag meals reportable to state
set    sttflg: 0;                     flag tips  reportable to state
set    sumnum: 0;                     work integer
set    subwag: 0;                     state wages
set    taxflg: 0
set    taxopt: 0;                     fed / state / city reporting option
set    templ:  0;                     total number of forms printed
set    wages:  0;                     total wages for empl
set    w2flag: 0;                     4-byte version w-2/1099 flag
set    pass:   0;                     pass number (0=first, 1=second)
set    nossno: 0;                     number of emps w/o a ss number
set    sempl:  0;                     sub-total check (41 max)
set    swages: 0;                     state wages
;
set    gropay: 0;                      gross pay + tips + meals...jh
;
define citnam: '        ' ; 8 duds
string citref (3)
string cittax (12)
string ftxbuf (11)
string null   (0)
string plan   (3)
string refcod (3)
string ssnum  (12)
define stanam: '        ' ; 8 duds
string staref (3)
string statax (12)
string str3   (3)
string zeropt (1)            ;12/09/82...jh  zero bal yes or no
;
end
END LIST
.USE CRT3 FOR SYSRDR
